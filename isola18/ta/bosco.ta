/* a template for BOSCO */

skel Proc {
  /* the control locations are as follows:
    0 is the initial state with value 0,
    1 is the initial state with value 1,
    2 is the state with value 0 sent,
    3 is the state with value 1 sent,
    4 is the state "decided on 0",
    5 is the state "decided on 1",
    6 is the state "called underlying consensus with value 0",
    7 is the state "called underlying consensus with value 1",
   */
  local pc;
  /* the number of messages sent by the correct processes */
  shared nsnt0 /* sent 0 */, nsnt1 /* sent 1 */, nsnt01 /* the sum */;
  parameters N, T, F;

  define THRESH1 == N - T;
  define THRESH2 == N + 3 * T;
  define THRESH3 == N + 3 * T;
  define THRESH4 == N - T;
  define THRESH5 == N - T;

  assumptions (1) {
    N > 3 * T;
    T >= F;
    T >= 1;
  }

  locations (7) {
    loc0: [0]; loc1: [1]; loc2: [2]; loc3: [3];
    loc4: [4]; loc5: [5]; loc6: [6]; loc7: [7];
  }

  inits (8) {
    (loc0 + loc1) == N - F;
    loc2 == 0; loc3 == 0; loc4 == 0;
    loc5 == 0; loc6 == 0; loc7 == 0;
    nsnt0 == 0; nsnt1 == 0; nsnt01 == 0;
  }

  rules (0) {
  /* send message 0 (resp. 1) when initialized with value 1 (resp. 1) */
  0: loc0 -> loc2
      when (true)
      do { nsnt0' == nsnt0 + 1; nsnt1' == nsnt1; nsnt01' == nsnt01 + 1; };
  1: loc1 -> loc3
      when (true)
      do { nsnt0' == nsnt0; nsnt1' == nsnt1 + 1; nsnt01' == nsnt01 + 1; };

  /* decide immediately */
  2: loc2 -> loc4 /* decide on value 0 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 >= THRESH2 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  3: loc3 -> loc5 /* decide on value 1 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt1 >= THRESH3 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };

  /* fall back to the underlying consensus with a vote */
  4: loc2 -> loc6 /* underlying consensus 0 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 >= THRESH4 - 2 * F)
                && (2 * nsnt1 < THRESH5))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  5: loc3 -> loc6 /* underlying consensus 0 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 >= THRESH4 - 2 * F)
                && (2 * nsnt1 < THRESH5))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  6: loc2 -> loc7 /* underlying consensus 1 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 < THRESH4)
                && (2 * nsnt1 >= THRESH5 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  7: loc3 -> loc7 /* underlying consensus 1 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 < THRESH4)
                && (2 * nsnt1 >= THRESH5 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };

  /* fall through with the initial value */
  8: loc2 -> loc6 /* underlying consensus 0 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 < THRESH4)
                && (2 * nsnt1 < THRESH5))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  9: loc2 -> loc6 /* underlying consensus 0 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 >= THRESH4 - 2 * F)
                && (2 * nsnt1 >= THRESH5 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  10: loc3 -> loc7 /* underlying consensus 1 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 < THRESH4)
                && (2 * nsnt1 < THRESH5))
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  11: loc3 -> loc7 /* underlying consensus 1 */
      when ((nsnt01 >= THRESH1 - F)
                && (2 * nsnt0 < THRESH2)
                && (2 * nsnt1 < THRESH3)
                && (2 * nsnt0 >= THRESH4 - 2 * F)
                && (2 * nsnt1 >= THRESH5 - 2 * F))
      do { unchanged(nsnt0, nsnt1, nsnt01); };

  /* self loops */
  12: loc0 -> loc0
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  13: loc1 -> loc1
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  14: loc2 -> loc2
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  15: loc3 -> loc3
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };

  16: loc4 -> loc4
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  17: loc5 -> loc5
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  18: loc6 -> loc6
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  19: loc7 -> loc7
      when (true)
      do { unchanged(nsnt0, nsnt1, nsnt01); };
  }

  specifications (0) {
    /* one-step safety */
    one_step0:
        ((F == 0 && N > 5 * T) || (N > 7 * T))
        -> (loc1 == 0 -> [](loc5 == 0 && loc6 == 0 && loc7 == 0));

    one_step1:
        ((F == 0 && N > 5 * T) || (N > 7 * T))
        -> (loc0 == 0 -> [](loc4 == 0 && loc6 == 0 && loc7 == 0));

    /* safety */
    lemmas34_0: []((loc4 != 0) -> [](loc5 == 0 && loc7 == 0));
    lemmas34_1: []((loc5 != 0) -> [](loc4 == 0 && loc6 == 0));

    /* the fairness constraints here should be encoded with respect to
       Byzantine faults, thus, we add -f, where needed */

    /* fast convergence */
    fast0:
        (((F == 0 && N > 5 * T) || (N > 7 * T))
            && <>[](((nsnt01 < THRESH1)
            || (2 * nsnt0 < THRESH2)
            || loc2 == 0)
                    && loc0 == 0 && loc1 == 0))
        ->
        (loc1 == 0
            -> <>(loc0 == 0 && loc1 == 0 && loc2 == 0 && loc3 == 0
                    && loc5 == 0 && loc6 == 0 && loc7 == 0));
    fast1:
        (((F == 0 && N > 5 * T) || (N > 7 * T))
            && <>[](((nsnt01 < THRESH1)
            || (2 * nsnt1 < THRESH3)
            || loc3 == 0)
                    && loc0 == 0 && loc1 == 0))
        ->
        (loc0 == 0
            -> <>(loc0 == 0 && loc1 == 0 && loc2 == 0 && loc3 == 0
                    && loc4 == 0 && loc6 == 0 && loc7 == 0));

    /* convergence (either fast or slow) */
    termination:
        (<>[]((
                (nsnt01 < THRESH1)
                || (2 * nsnt0 < THRESH2)
                || (2 * nsnt1 < THRESH3)
                || (2 * nsnt0 < THRESH4)
                || (2 * nsnt0 < THRESH5)
                || (loc2 == 0 && loc3 == 0))
            && loc0 == 0 && loc1 == 0))
        ->
        <>(loc0 == 0 && loc1 == 0 && loc2 == 0 && loc3 == 0);
  }
} /* Proc */


